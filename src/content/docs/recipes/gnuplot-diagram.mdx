---
title: Gnuplot diagram
tags: [markdown, code-fences, diagram]
---

```gnuplot
# Plotting filledcurves with different transparencies
#
# AUTHOR: Hagen Wierstorf

set samples 1000, 500

set border linewidth 1.5

# Axes

set style line 11 lc rgb '#808080' lt 1
set border 3 back ls 11
set tics nomirror out scale 0.75

# Grid

set style line 12 lc rgb'#808080' lt 0 lw 1
set grid back ls 12

set style fill noborder
set style function filledcurves y1=0
set clip two

Gauss(x,mu,sigma) = 1./(sigma*sqrt(2*pi)) * exp( -(x-mu)**2 / (2*sigma**2) )
d1(x) = Gauss(x, 0.5, 0.5)
d2(x) = Gauss(x, 2., 1.)
d3(x) = Gauss(x, -1., 2.)

set xrange [-5:5]
set yrange [0:1]

unset colorbox

set key title "Gaussian Distribution"
set key top left Left reverse samplen 1

set lmargin 6
plot d1(x) fs transparent solid 0.75 lc rgb "forest-green" title 'µ= 0.5 σ=0.5', \
 d2(x) fs transparent solid 0.50 lc rgb "gold" title 'µ= 2.0 σ=1.0', \
 d3(x) fs transparent solid 0.25 lc rgb "red" title 'µ=-1.0 σ=2.0'
```

Example taken [here](http://gnuplotting.org/filledcurves-with-different-transparency/index.html)

## About

[Gnuplot](https://gnuplot.sourceforge.net/) is a graphing utility. The source code is copyrighted but freely distributed (i.e., you don't have to pay for it). It was originally created to allow scientists and students to visualize mathematical functions and data interactively. It is also used as a plotting engine by third-party applications like Octave. Gnuplot has been supported and under active development since **1986**.

## Installation

import { Steps } from "@astrojs/starlight/components";

<Steps>
1. Install dependencies

    ```bash
    pnpm add @beoe/rehype-gnuplot
    ```

2. Configure Astro. See note about [[rehype-plugins-for-code]].

   ```js {2, 6}
   // astro.config.mjs
   import { rehypeGnuplot } from "@beoe/rehype-gnuplot";

   export default defineConfig({
     markdown: {
       rehypePlugins: [rehypeGnuplot],
     },
   });
   ```

3. **Optional** install dependency for cache

   ```bash
   pnpm add @beoe/cache
   ```

4. **Optional** configure cache

   ```js {2, 4, 8}
   // astro.config.mjs
   import { getCache } from "@beoe/cache";

   const cache = await getCache();

   export default defineConfig({
     markdown: {
       rehypePlugins: [rehypeGnuplot, { cache }],
     },
   });
   ```

</Steps>

## Example

````md
```gnuplot
plot [-10:10] sin(x)
```
````

```gnuplot
plot [-10:10] sin(x)
```

## Bonus: XKCD-style

```gnuplot
# Gnuplot file that plots a couple of functions in the xkcd style
# Not a proper rendition, as the axis are still straight lines
# Also, when plotting functions, the jiggling can only be done in
# the y coordinate. For datapoints, one can do the jiggling on both
# x and y.
# The proper way to do this would be to write a xkcd terminal for
# gnuplot, but this is way beyond the time that I can use on this...

# You can download the HumorSans font from here: http://antiyawn.com/uploads/Humor-Sans.ttf
# To use it in the eps terminal, you have to convert it to a postscript pfa format
# Since I already did that, you can just use the file included in this git repository.

set terminal svg enhanced font "HumorSans,Patrick Hand,Chalkboard,Comic Sans MS,18"

# Some line types
set style line 10 lt 1 lc rgbcolor "#ffffff" lw 8 #thick white
set style line 11 lt 1 lc rgbcolor "#000000" lw 2 #black

set style line 1 lt 1 lc rgbcolor "#ff0000" lw 2  #red
set style line 2 lt 1 lc rgbcolor "#0000ff" lw 2  #blue

# No border with tics
set border 0

set noxtics
set noytics

# Show the axis
set xzeroaxis ls 11
set yzeroaxis ls 11

#Arrow end to the axis
set arrow from graph 0.95, first 0 to graph 1, first 0 size 2,3 front
set arrow from first 0, graph 0.95 to first 0, graph 1 size 2,3 front

set yrange [-1.1:1.1]
set xrange [-1:15]

set key bottom

set label 'Wasted time' at 11,0.7 right
set arrow from 11.1,0.7 to 12,((12/15.0)**2) ls 11

set title 'Check this out! XKCD in Gnuplot'

#Jiggling functions
range = 2.0                                #Range for the absolute jiggle
jigglea(x) = x+range*(2*(rand(0)-0.5)*0.005)    #Absolute (as a fraction of the range)
jiggle(x) = x*(1+(2*rand(0)-0.5)*0.015)         #Relative +-1.5% (as a fraction of the y value)

dpsin(x) = sin(x) * exp(-0.1 * (x - 5) ** 2)
dpcos(x) = - cos(x) * exp(-0.1 * (x - 5) ** 2)

plot  jiggle(dpsin(x)) ls 10 t '', \
      jiggle(dpsin(x)) ls 1 t 'Damped Sin',\
      jiggle(dpcos(x)) ls 10 t '', \
      jiggle(dpcos(x)) ls 2 t 'Damped Cos',\
      jigglea((x/15)**2) ls 10 t '',\
      jigglea((x/15)**2) ls 11 t ''
```

Example taken [here](https://rfonseca.github.io/xkcd-gnuplot/)

**Note**: in order for example to work as SVG I changed following lines:

```diff
- set term postscript eps font 'HumorSans' fontfile 'Humor-Sans.pfa' 22
- set output 'xkcd.eps'
+ set terminal svg enhanced font "HumorSans,Patrick Hand,Chalkboard,Comic Sans MS,18"
```

You may use any font you like and load if, for example:

- [Humor Sans](https://github.com/shreyankg/xkcd-desktop/blob/master/Humor-Sans.ttf)
- [Patrick Hand](https://fonts.google.com/specimen/Patrick+Hand)
